class Paddle {

    field int x, y; // the paddle's screen location
    field int width, height; // the paddle's width and height
    field int direction; // direction of the paddle movement (1 = left, 2 = right)

    /** Constructs a new paddle with the given location and width. */
    constructor Paddle new(int Ax, int Ay, int Awidth, int Aheight) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let direction = 0;
        do show();
        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Shows the paddle. */
    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    /** Hides the paddle. */
    method void hide() {
        do Screen.setColor(false);
        do draw();
        return;
    }

    /** Draws the paddle. */
    method void draw() {
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    /** Sets the paddle's direction (0=stop, 1=left, 2=right). */
    method void setDirection(int Adirection) {
        let direction = Adirection;
        return;
    }

    /** Returns the paddle's left edge. */
    method int getLeft() {
        return x;
    }

    /** Returns the paddle's right edge. */
    method int getRight() {
        return x + width;
    }

    /** Moves the bat one step in the bat's direction. */
    method void move() {
        if (direction = 1) {
            let x = x - 4;
            if (x < 0) { let x = 0; } // beside left wall
            do Screen.setColor(false);
            do Screen.drawRectangle((x + width) + 1, y, (x + width) + 4, y + height);
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 3, y + height);
        }
        else {
            let x = x + 4;
            if ((x + width) > 511) { let x = 511 - width; } // beside right wall
            do Screen.setColor(false);
            do Screen.drawRectangle(x - 4, y, x - 1, y + height);
            do Screen.setColor(true);
            do Screen.drawRectangle((x + width) - 3, y, x + width, y + height);
        }
        return;
    }
}
