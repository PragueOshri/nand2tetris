// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // increment the current bit
    // so we can take the result if inc == 1
    Inc16 (in=current, out=incOut);

    Mux16 (a=current, b=incOut, sel=inc, out=out1);
    Mux16 (a=out1, b=in[0..15], sel=load, out=out2);
    Mux16 (a=out2, b=false, sel=reset, out=out3);

    // load == 1 because in the Mux before, we check
    // all the control bits one after another
    Register (in=out3, load=true, out=out, out=current);
}
